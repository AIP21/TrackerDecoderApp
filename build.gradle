buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        google()
    }
}

plugins {
    id 'java'
    id "application"
    id 'edu.sc.seis.launch4j' version '2.5.3'
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

repositories {
    mavenCentral()
    google()
    maven {
        url "https://maven.latticg.com/"
    }
    maven {
        url "https://maven.seedfinding.com/"
    }
    maven {
        url "https://maven-snapshots.seedfinding.com/"
    }
}

defaultTasks("clean", "release")

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(16))
    }
}

task createProperties() {
    doLast {
        new File("$buildDir/resources/main/version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p.store w, null
        }
        // copy needed, otherwise the bean VersionController can't load the file at startup when running complete-app tests.
        copy {
            from "$buildDir/resources/main/version.properties"
            into "src/main/resources"
        }
    }
}

classes {
    dependsOn createProperties
}

task("release") {
    dependsOn(createProperties)
    dependsOn(clean)
    dependsOn(shadowJar)
    dependsOn(createExe)
}

application {
    mainClass.set("src/main/java/main/PlayerTrackerDecoder.java")
}

//tasks.withType(JavaCompile) {
//    options.compilerArgs += ['-Xlint:unchecked', '-Xlint:deprecation']
//}

dependencies {
    implementation('com.seedfinding:mc_core:706e4f1b7aa6b42b3627f682a311d06280d80b5c') { transitive = false }
    implementation('com.seedfinding:mc_seed:5518e3ba3ee567fb0b51c15958967f70a6a19e02') { transitive = false }
    implementation('com.seedfinding:mc_biome:b2271807a047bb43ac60c8c20ad47e315f19b9a6') { transitive = false }
    implementation('com.seedfinding:mc_terrain:9e937ddb838e28e79423c287fa18b1ce66f061d7') { transitive = false }
    implementation('com.seedfinding:mc_noise:a6ab8e6c688491829f8d2adf845392da22ef8e9c') { transitive = false }
    implementation('com.seedfinding:mc_math:d73ac7cc644c67628ade0effd7136e11eb00bb76') { transitive = false }

    implementation('com.seedfinding:latticg:1.06')

    implementation('com.formdev:flatlaf:2.3')
    implementation('com.google.code.gson:gson:2.9.0')
}

run {
    args "-no-update"
}

shadowJar {
    archiveFileName = "${project.name}-${project.version}-FR.jar"
}

launch4j {
    outfile = "${project.name}-${project.version}-FR.exe"
    icon = "${projectDir}/icon.ico"
    splashFileName = "${projectDir}/splashImg.bmp"

    productName = "WAT:Vis"
    fileDescription = "A tool for game developers and server owners to decode, visualize, and analyze player positional data."

    jvmOptions = ["-Xms1G", "-Xmx8G"]

    jarTask = project.tasks.shadowJar
    jreMaxVersion = JavaVersion.VERSION_HIGHER
    jreMinVersion = JavaVersion.VERSION_16
    bundledJrePath = "%JAVA_HOME%;%PATH%"
}